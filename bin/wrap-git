#!/usr/bin/env perl

use strict;
use warnings;

use Net::Netrc;

$0 = 'git';

my %aliases = map { split(/\n/, $_, 2) }
   split /\0/,
   `git config -z --get-regexp alias\\.`;

my %commit_aliases = (( commit => 1 ),
   map { s/alias\.//; $_ => 1 }
   grep $aliases{$_} =~ /^commit\b/,
   keys %aliases);

my %checkout_aliases = (( checkout => 1 ),
   map { s/alias\.//; $_ => 1 }
   grep $aliases{$_} =~ /^checkout\b/,
   keys %aliases);

my %sendemail_aliases = (( 'send-email' => 1 ),
   map { s/alias\.//; $_ => 1 }
   grep $aliases{$_} =~ /^send-email\b/,
   keys %aliases);

exec 'git' unless @ARGV;

my ($command, @args) = @ARGV;

if ($commit_aliases{$command} && exists $args[0] && $args[0] =~ /^-a|^--all/) {
   my @staged = grep /^M/, split /\0/, `git status -z`;
   if (@staged) {
      print "There are staged changes, are you sure you want to commit all? (y/N) ";
      chomp(my $answer = <STDIN>);
      if ($answer =~ /^y/i) {
         run_command()
      }
   } else {
      run_command()
   }
} elsif ($checkout_aliases{$command} && ( grep /^-b|^-B/, @args ) && ( grep /^origin\/(release|master)/, @args )) {
   unshift @args, '--no-track';
   run_command()
} elsif ($sendemail_aliases{$command}) {
   unshift @args, '--smtp-pass', Net::Netrc->lookup('smtp.gmail.com')->password;
   run_command()
} elsif ($command eq 'pull' && !grep /^-/, @args) {
   unshift @args, '--ff-only';
   run_command()
} else {
   run_command()
}

sub run_command {
   system 'git', $command, @args;
   exit $? >> 8;
}
