#!/usr/bin/env perl
use 5.16.0;
use warnings;

use Mail::IMAPClient;
use Lingua::EN::Inflect 'PL';

use Getopt::Long::Descriptive;

my ($opt, $usage) = describe_options(
  "$0 %o",
  [ 'server|s=s'      , "the server to connect to"                                                   ] ,
  [ 'password|p=s'    , "the password to use"                                                        ] ,
  [ 'user|u=s'        , "the username to use"                                                        ] ,
  [ 'label|l=s'       , "the label to check"                                                         ] ,
  [ 'urgency|U=s'     , "Specifies the urgency level (low, normal, critical)"                        ] ,
  [ 'expire-time|t=i' , "Specifies the timeout in milliseconds at which to expire the notification." ] ,
  [],
  [ 'help|h',       "print usage message and exit" ],
);

  print($usage->text), exit if $opt->help;

my $imap = Mail::IMAPClient->new(
  Server   => $opt->server,
  User     => $opt->user,
  Password => $opt->password || $ENV{CHECK_PASSWORD},
  Ssl      => 1,
  Uid      => 1,
) or die "new failed: $@\n";

my $label = $opt->label;
my $count = $imap->message_count( $label )
  // die "Select '$label' error: $@\n";

if ($count) {
   system(
      'notify-send',
      '-t', $opt->expire_time,
      '-u', $opt->urgency,
      "$count $label-level " . PL('email', $count) . "!"
   );
}

# alert
# crit
# err
# warn
