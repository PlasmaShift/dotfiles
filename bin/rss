#!/usr/bin/env perl

use 5.14.1;
use warnings;
use JSON 'encode_json', 'decode_json';
use LWP::UserAgent;
use Try::Tiny;

my ($ua, $sid);

sub help {
   print <<"HELP";
Examples:

   $0 raw getCategories

   $0 raw subscribeToFeed feed_url https://example.com/feed.atom category_id 123

See https://tt-rss.org/gitlab/fox/tt-rss/wikis/ApiReference
HELP
}

sub raw {
   my (undef, $method, @args) = @_;

   print rss_api(
      $method => (
         sid => _sid(),
         @args,
      ),
   )

}

my $method = shift;
if (!$method || $method =~ m/^-/) {
   help();
   exit;
}

main->$method(@ARGV);

sub _ua { $ua ||= LWP::UserAgent->new; $ua }
sub _sid {
   my $str = rss_api( login => () );
   $sid ||= try { decode_json($str)->{content}{session_id} } catch { die "Invalid JSON: $str\n" };

   $sid
}

sub rss_api {
    my ($method, @args) = @_;
    my $request = HTTP::Request->new(
      POST => "$ENV{RSS_URL}/api/",
      [], encode_json({ op => $method, @args })
    );
    $request->authorization_basic( $ENV{RSS_USER} => $ENV{RSS_PASS} );
    my $response = _ua()->request($request);

    return $response->decoded_content
}
